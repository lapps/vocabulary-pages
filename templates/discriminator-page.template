/*
 * Template used to generate a single HTML page for a discriminator.
 *
 * MAGIC VARIABLES
 *
 * page: an instance of org.lappsgrid.discriminator.generators.Page is inject
 *       which contains information about the discriminator.
 * version: the value of the -V command line parameter.
 */

makeLinks = { String text ->
	if (text == null) return text
	return text.replaceAll("<link>(.*?)</link>", "<a href='\$1'>\$1</a>")
}

html {
    head {
        title "${page.name}"
        link rel:'stylesheet', type:'text/css', href:'http://vocab.lappsgrid.org/css/discriminators.css'
    }
    body(id:'container') {
        div(id:'intro') {
            div(id:'pageHeader') {
                h1 'LAPPS Web Service URI Inventory'
                h2 "${page.name} v${version}"
            }
        }
        div(id:'mainContent') {
            //h1 "${page.name}"
            p class:'note', 'This information is posted for comment and discussion only.'
            p class:'note', 'This file is machine generated and should not be edited.'
            def discriminators = page.discriminators
            def first = discriminators[0]
            h2 first.discriminator.uri
            p first.discriminator.description

            if (discriminators.size() > 1) {
                ul {
                    discriminators[1..-1].each { d ->
                        li {
                            a(href:d.fragment, d.fragment.substring(1))
                        }
                    }
                }
                discriminators[1..-1].each { d ->
                    a name:"${d.fragment.substring(1)}", ''
                    h2 d.discriminator.uri
		    		mkp.yieldUnescaped "\n      <p>"
				    mkp.yieldUnescaped makeLinks(d.discriminator.description)
				    mkp.yieldUnescaped "</p>"
                }
            }
			div {
				br()
				br()
				a(rel:'license', href:'http://creativecommons.org/licenses/by/4.0/') {
					img(alt:"Creative Commons License", style:"border-width:0; vertical-align: middle;", src:"https://i.creativecommons.org/l/by/4.0/88x31.png", '')
				}
				span {
					span "The LAPPS Grid Vocabulary is licensed under the "
					a(rel:"license", href:"http://creativecommons.org/licenses/by/4.0/", 'Creative Commons Attribution 4.0 International License')
					span('.')
				}
				br()
				br()            
			}                
        }
        div(class:'footer') {
            def year = Calendar.getInstance().get(Calendar.YEAR)
            mkp.yieldUnescaped "<p>Copyright &copy; ${year} The Language Application Grid. All rights reserved.</p>"
        }
    }
}